// Bootstrap form App

{text, type ClassProp, type VNode} from hyperapp
* as html from @hyperapp/html

InputLayout ::=
  | "normal"
  | "floating"

InputTypeAttribute ::=
  | "button"
  | "checkbox"
  | "color"
  | "date"
  | "datetime-local"
  | "email"
  | "file"
  | "hidden"
  | "image"
  | "month"
  | "number"
  | "password"
  | "radio"
  | "range"
  | "reset"
  | "search"
  | "submit"
  | "tel"
  | "text"
  | "time"
  | "url"
  | "week"

// Views

export input := <S>({layout:: InputLayout = "normal", ...props:: any}): VNode<S> ->
  switch layout
    when "normal"
      input_noraml props
    when "floating"
      input_floating props
    else
      throw new Error(`unkonwn layout: ${layout}`)

input_noraml := <S>({
  id:: string
  label:: string
  type:: InputTypeAttribute
  valid?:: string
  invalid?:: string
  help?:: string
  class: klass?:: ClassProp
  wrap_class?:: ClassProp
  ...props:: any
}): VNode<S> ->
  input_class := [
    klass
    "form-control"
    {is-valid: valid, is-invalid: invalid}
  ]
  helpId := `${id}-help` if help

  html.div {class: [wrap_class, "mb-3"]}, [
    html.label {class: "form-label", for: id}, text label
    html.input {
      id, type, class: input_class
      aria-describedby: helpId
      ...props
    }
    html.div {class: "invalid-feedback"}, text valid if valid
    html.div {class: "invalid-feedback"}, text invalid if invalid
    html.div {id: helpId, class: "form-text"}, text help if help
  ]

input_floating := ({
  id:: string
  label:: string
  type:: InputTypeAttribute
  valid?:: string
  invalid?:: string
  help?:: string
  class: klass?:: ClassProp
  wrap_class?:: ClassProp
  ...props:: any
}) ->
  input_class := [
    klass
    "form-control"
    {is-valid: valid, is-invalid: invalid}
  ]
  helpId := `${id}-help` if help

  html.div {class: [wrap_class, "form-floating mb-3"]}, [
    html.input {
      ...props
      id, type, class: input_class
      aria-describedby: helpId
      placeholder: label
    }
    html.label {for: id}, text label
    html.div {class: "invalid-feedback"}, text valid if valid
    html.div {class: "invalid-feedback"}, text invalid if invalid
    html.div {id: helpId, class: "form-text"}, text help if help
  ]

