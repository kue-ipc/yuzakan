// Status Icon app

type {ElementVNode} from hyperapp
{text} from hyperapp
* as html from @hyperapp/html
bsIcon from ~/app/bs_icon.civet
Status from ~/common/status.civet

// Hyperapp Views

export statusIcon := <S>({status, class: klass = []}: {status: string | Status,
    class: string | string[]}): ElementVNode<S> ->
  if typeof status is "string"
    status = Status.get(status)

  if status.spinner?
    html.div {class: [`spinner-${status.spinner}`, klass], role: "status"},
      html.span {class: "visually-hidden"},
        text status.label
  else if status.icon?
    html.div {class: [`text-${status.color}`, klass]},
      bsIcon {name: status.icon, alt: status.label ?? status.name, size: 32}
  else
    html.div {class: [`text-${status.color}`, klass]},
      text status.label
