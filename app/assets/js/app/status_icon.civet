// Status Icon app

{text, type ClassProp} from hyperapp
* as html from @hyperapp/html
icon from ~/app/icon.civet
Status from ~/common/status.civet
{bsTextColorClass} from ~/common/bs.civet

// Hyperapp Views

export statusIcon := <S>({
  status:: string | Status
  class: klass:: ClassProp = []
}) ->
  if typeof status is "string"
    status = Status.get(status)

  divClass := [bsTextColorClass(status.color), klass]
  if status.spinner?
    html.div<S> {class: [`spinner-${status.spinner}`, divClass], role: "status"},
      html.span<S> {class: "visually-hidden"},
        text status.label
  else if status.icon?
    html.div<S> {class: divClass},
      icon<S> {name: status.icon, alt: status.label ?? status.name, size: 32}
  else
    html.div<S> {class: divClass},
      text status.label
