// load loacle

{I18N_TRANSLATIONS_KEY} from ~/common/i18n.civet

LOCALE_PATH := "/locale"

export loadLocale := (): string ->
  currentTranslations := localStorage.getItem(I18N_TRANSLATIONS_KEY)
  return currentTranslations if currentTranslations?

  console.debug "fetch locale: %s", LOCALE_PATH
  response := await fetch(LOCALE_PATH)
  console.debug "response locale: %d %s", response.status, response.statusText
  unless response.ok
    throw new Error(`failed to fetch locale: ${response.statusText}`)
  responseContentType := response.headers.get("Content-Type")
  console.debug "response locale content type: %s", responseContentType
  unless responseContentType?.split(";")[0].trim() is "application/json"
    throw new Error(`response is not json for locale: ${responseContentType}`)

  translations := await response.text()
  localStorage.setItem(I18N_TRANSLATIONS_KEY, translations)
  translations

export waitLoadLocale := (): Promise<string> ->
  new Promise<string> (resolve, reject) ->
    currentTranslations := localStorage.getItem(I18N_TRANSLATIONS_KEY)
    return resolve(currentTranslations) if currentTranslations

    checkTranslation := (e: StorageEvent) ->
      return unless e.key is I18N_TRANSLATIONS_KEY
      return unless e.newValue?

      window.removeEventListener "storage", checkTranslation
      resolve(e.newValue)
    
    window.addEventListener "storage", checkTranslation
    console.debug "wait load locale"
