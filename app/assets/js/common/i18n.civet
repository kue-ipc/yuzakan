// I18n

LOCALE_PATH := "/locale"

translationStore: any .= null

fetchTranslation := (): Promise<{}> ->
  response := await fetch(LOCALE_PATH, {
    headers: {Accept: "application/json"},
  })
  translationStore = await response.json()
  translationStore

export t := (key: string, opts: {[key: string]: string} = {}): string ->
  unless translationStore?
    console.warn "not yet get a translation"
    return key

  current: any .= translationStore
  for each str of key.split(".")
    current = current[str]
    break unless current?

  unless current <? "string"
    console.warn "no translaiton fond: %s", key
    return key

  for own name, replace in opts
    current = current.replaceAll(`%{${name}}`, replace)
  
  current

// fetch translationStore
fetchTranslation()
