// Theme - Bootstrap color modes (themes)

{t} from ./i18n.civet

export default class Theme
  readonly #name: string
  readonly #icon: string

  @({#name:: string, #icon:: string})

  get #name
  get #icon
  get label
    t(`ui.themes.${#name}`)
  get theme_name
    if #name is "auto"
      if window.matchMedia("(prefers-color-scheme: dark)").matches
        "dark"
      else
        "light"
    else
      #name

  apply()
    document.documentElement.setAttribute("data-bs-theme", @theme_name)

  @DEFAULT_THEME = new Theme({name: "auto", icon: "circle-half"})

  @LIST: Theme[] := [
      {name: "light", icon: "sun-fill"}
      {name: "dark", icon: "moon-stars-fill"}
    ].map((params) -> new Theme(params)).concat([@DEFAULT_THEME])

  @MAP: Map<string, Theme> := new Map(@LIST.map((theme) -> [theme.name, theme]))

  @THEME_STORED_KEY := "theme"

  @get(name: string): Theme
    @MAP.get(name) || @DEFAULT_THEME

  @get storedTheme(): Theme
    if themeName := localStorage.getItem(@THEME_STORED_KEY)
      @get(themeName)
    else
      @DEFAULT_THEME

  @set storedTheme(theme: Theme)
    localStorage.setItem(@THEME_STORED_KEY, theme.name)
