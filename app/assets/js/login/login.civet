// Login

{app, text, type Dispatch} from hyperapp
* as html from @hyperapp/html
{focus} from @hyperapp/dom
{delay} from @hyperapp/time
APIDialog from ~/api_dialog.civet
bsIcon from ~/app/bs_icon.civet
csrf from ~/csrf.civet
{t, waitLoadTranslations} from ~/common/i18n.civet
{normalizeURL} from ~/helpers/url_helper.civet

type LoginState
  username: string??
  password: string??
  disabled: boolean

await waitLoadTranslations()

apiDialog := new APIDialog {
  id: "login"
  name: t("actions.login")
  method: "POST"
  path: "session"
  responseMessageCodeMap: new Map [
    [201, {status: "success", message: "login_succeeded",
      redirectTo: normalizeURL("/")}]
    [400, {status: "failure", message: "login_failed"}]
    [422, {status: "failure", message: "login_failed"}]
  ]
}

submittable := ({username, password, disabled}: LoginState) ->
  not disabled and username and password

// Effecters

runLogin := (dispatch: Dispatch<LoginState>, payload: {username: string??, password: string??}) ->
  await apiDialog.submitPromise {data: {
    ...csrf()
    ...payload
  }}
  dispatch (state) -> [
    {
      ...state
      disabled: false
      password: null
    }
    focus("session-username")
  ]

// Effect Generators

login := ({username, password, disabled}: LoginState) ->
  [runLogin, {username, password}]

// Actions

EnterToSubmitOrNextInput := (state: LoginState, event: KeyboardEvent) ->
  if event.keyCode is 13
    if submittable(state)
      [{...state, disabled: true}, login(state)]
    else if event.target? and event.target <? Element and
        event.target.id is "session-username"
      [state, focus("session-password")]
    else
      [state, focus("session-username")]
  else
    state

UpdateUsername := (state: LoginState, event: InputEvent): LoginState ->
  if event.target <? HTMLInputElement
    {...state, username: event.target.value}
  else
    console.warn "username event target is not html input element"
    state

UpdatePassword := (state: LoginState, event: InputEvent): LoginState ->
  if event.target <? HTMLInputElement
    {...state, password: event.target.value}
  else
    console.warn "password event target is not html input element"
    state

Login := (state: LoginState, event: PointerEvent) ->
  [{...state, disabled: true}, login(state)]


// Parameters

init := [
  {
    disabled: false
    username: null
    password: null
  }
  delay 10, (state: LoginState) -> [state, focus("session-username")]
]

view := ({username, password, disabled}: LoginState) ->
  html.div {id: "login", class: "login mx-auto p-3 border rounded"}, [
    html.h3 {class: "login-title text-center mb-2"}, text t("actions.login")
    html.div {class: "mb-3"},
      html.input {
        id: "session-username"
        class: "form-control"
        type: "text"
        required: true
        placeholder: t("view.labels.username")
        disabled
        value: username
        oninput: UpdateUsername
        onkeypress: EnterToSubmitOrNextInput
      }
    html.div {class: "mb-3"},
      html.input {
        id: "session-password"
        class: "form-control"
        type: "password"
        required: true
        placeholder: t("view.labels.password")
        disabled
        value: password
        oninput: UpdatePassword
        onkeypress: EnterToSubmitOrNextInput
      }
    html.div {class: "d-grid gap-auto"},
      html.button {
        class: "login-submit btn btn-primary d-flex align-items-center justify-content-center"
        disabled: not submittable({username, password, disabled})
        onclick: Login
      }, [
        bsIcon {name: "box-arrow-in-left", class: "flex-shrink-0 me-1"}
        text t("actions.login")
      ]
  ]

node := document.getElementById("login")

if node?
  app<LoginState> {init, view, node}
