{basename} from ./file_helper.civet

export getCurrentURLPath := (): string ->
  globalThis.location.href

export getCurrentURL := (): URL ->
  new URL(globalThis.location.href)

export normalizeURL := (url: URL | string, base?: URL | string): Readonly<URL> ->
  if url <? "string"
    Object.freeze(new URL(url, base ?? getCurrentURLPath()))
  else if Object.isFrozen(url)
    url
  else
    Object.freeze(new URL(url.href))

export getBasenameFromURL := (url: URL | string, suffix: string =""): string ->
  basename(normalizeURL(url).pathname, suffix)

export getQueryParamsFromURL := (url: URL | string): Map string, string ->
  new Map(new URLSearchParams(normalizeURL(url).search))

export relativePath := (url: URL, base: URL = getCurrentURL()): string ->
  url.href.replace(base.origin, "")
