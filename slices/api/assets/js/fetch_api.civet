/**
 * fetchAPI
 * APIと連携してデータをJSONで受け取る処理をする。
 */

{formDataToJson, formDataToUrlencoded, objToUrlencoded} from
  ~/helpers/form_helper.civet
{objToJson} from ~/helpers/obj_helper.civet
{normalizeURL} from ~/helpers/url_helper.civet
Pagination from ~api/pagination.civet

export Method ::= "GET" | "HEAD" | "POST" | "PUT" | "PATCH" | "DELETE"

export interface RequestParams < RequestInit
  path: string | string[]
  methed?: Method
  data?: any

export interface ResponseData
  ok: boolean
  status: {code: number, message: string}
  location: URL
  csrf?: Record<string, string>
  flash?: Record<string, string | Record<string, string[]>>
  data?: any
  pagination?: Pagination

API_URL := normalizeURL("/api/")

export fetchAPI := (params: RequestParams): Promise<ResponseData> ->
  request := createRequest(params)
  console.debug "fetch request: %s %s", request.method, request.url
  response := await fetch request
  console.debug "fetch response: %d %s", response.status, response.url
  parseResponse(response)

export fetchAPIGet := (params: RequestParams) ->
  fetchAPI({...params, method: "GET"})

export fetchAPIHead := (params: RequestParams) ->
  fetchAPI({...params, method: "HEAD"})

export fetchAPIPost := (params: RequestParams) ->
  fetchAPI({...params, method: "POST"})

export fetchAPIPut := (params: RequestParams) ->
  fetchAPI({...params, method: "PUT"})

export fetchAPIPatch := (params: RequestParams) ->
  fetchAPI({...params, method: "PATCH"})

export fetchAPIDelete := (params: RequestParams) ->
  fetchAPI({...params, method: "DELETE"})

normalizePath := (path: string | string[]) ->
  path = path.split("/") if path <? "string"
  path.map(encodeURIComponent).join("/")

apiURL := (path: RequestParams["path"]): URL ->
  new URL(normalizePath(path), API_URL)

// create request
createRequest := ({path, method = "GET", data, ...params}: RequestParams):
    Request ->
  url := apiURL(path)
  headers := new Headers {}
    "Accept": "application/json"

  let body
  if data?
    switch method
      when "GET", "HEAD"
        query :=
          if data <? "string"
            encodeURIComponent(data)
          else if data <? FormData
            formDataToUrlencoded(data)
          else
            objToUrlencoded(data)
        url.search = query
      when "POST", "PUT", "PATCH", "DELETE"
        body =
          if data <? "string"
            data
          else if data <? FormData
            formDataToJson(data)
          else
            objToJson(data)
        headers.append("Content-Type", "application/json")
        headers.append("Content-Length", body.length.toString())

  init: RequestInit := {}
    mode: "same-origin"
    credentials: "same-origin"
    ...params
    method: method
    headers
    body

  new Request(url, init)

parseResponse := (response: Response): Promise<ResponseData> ->
  mimeType := response.headers.get("Content-Type")?.split(";")[0]?.trim()
    ?.toLowerCase()
  if mimeType is not "application/json"
    throw new Error("response is not json")

  data := await response.json()
  responseData: ResponseData := {}
    ok: response.ok
    status: data.status as ResponseData["status"]
    location: normalizeURL(data.location)

  responseData.csrf = data.csrf if data.csrf?
  responseData.flash = data.flash if data.flash
  responseData.data = data.data if data.data
  responseData.pagination =
    new Pagination(data.paginitaion) if data.paginitaion?
  
  responseData
