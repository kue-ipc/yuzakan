{text} from hyperapp
* as html from @hyperapp/html

{entityLabel} from ~/common/helper.civet

bsIcon from ~/app/bs_icon.civet
valueDisplay from ~/app/value_display.civet

PROVIDER_REG_ITEMS := [
  {name: "username", label: "ユーザー名", type: "string"}
  {name: "display_name", label: "表示名", type: "string"}
  {name: "email", label: "メールアドレス", type: "string"}
  {name: "locked", label: "ロック", type: "boolean"}
  {name: "unmanageable", label: "管理不可", type: "boolean"}
  {name: "mfa", label: "多要素", type: "boolean"}
]

userAddProviderAction := (state, {provider_name}) ->
  if state.user.providers.includes(provider_name)
    state
  else
    {
      ...state
      user: {
        ...state.user
        providers: [...state.user.providers, provider_name]
      }
    }

userRemoveProviderAction := (state, {provider_name}) ->
  {
    ...state
    user: {
      ...state.user
      providers: state.user.providers.filter (item) -> item is not provider_name
    }
  }

providerCheck := ({provider_name, checked, edit := false}) ->
  if edit
    html.div {class: "form-check"},
      html.input {
        id: `provider-${provider_name}`
        class: "form-check-input"
        type: "checkbox"
        checked
        onchange:
          if checked
            [userRemoveProviderAction, {provider_name}]
          else
            [userAddProviderAction, {provider_name}]
      }
  else
    if checked
      html.span {class: `text-success`},
        bsIcon({name: "check-square"})
    else
      html.span {class: `text-muted`},
        bsIcon({name: "square"})

providerTd := ({user, provider, name, type}) ->
  return html.td {} unless user.providers.includes(provider.name)

  data := user.providers_data.get(provider.name)

  html.td {},
    valueDisplay {
      value: data?[name]
      type
      color:
        if user[name]
          if user[name] is data?[name]
            "success"
          else
            "danger"
        else
          "body"
    }

providerTr := ({user, providers, name, label, type}) ->
  html.tr {}, [
    html.th {}, text label
    ...(providerTd {user, provider, name, type} for each provider of providers)
  ]

providerCheckTd := ({mode, user, provider}) ->
  html.td {},
    providerCheck {
      provider_name: provider.name
      checked: user.providers.includes(provider.name)
      edit: mode is not "show"
    }

export default userProvider := ({mode, user, providers}) ->
  html.div {}, [
    html.h4 {}, text "プロバイダー登録状況"
    html.table {class: "table"}, [
      html.thead {},
        html.tr {}, [
          html.th {}, text ""
          ...(html.th({}, text entityLabel(provider)) for each provider of providers)
        ]
      html.tbody {}, [
        html.tr {}, [
          html.th {}, text "登録"
          ...(providerCheckTd({mode, user, provider}) for each provider of providers)
        ]
        ...(if mode is not "new" then (providerTr {user, providers, ...item} for each item of PROVIDER_REG_ITEMS) else [])
      ]
    ]
  ]
